/* Detect Walking Speeds install */
COPY_EXISTING ~_montar.cre~ ~override~
  READ_BYTE  0x33 "eff_version"
  READ_LONG  0x2c4 "effects_offset"
  READ_LONG  0x2c8 "fx_num"
  SET "adjust_speed" = 0
  PATCH_IF ("%eff_version%" = 0) BEGIN
    FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
      READ_SHORT ("%effects_offset%" + ("%loops%" * 0x30)) "type"
      PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
        SET "loops" = "%fx_num%"
        SET "adjust_speed" = 1
      END
    END
  END ELSE BEGIN
    FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
      READ_LONG ("%effects_offset%" + 8 + ("%loops%" * 264)) "type"
      PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
        SET "loops" = "%fx_num%"
        SET "adjust_speed" = 1
      END
    END
  END
BUT_ONLY_IF_IT_CHANGES

/* Patching BG1NPC added creatures for Walking Speeds */
COPY_EXISTING_REGEXP GLOB  ~^[XP]#.*\.cre$~ ~override~
READ_BYTE  0x33  "eff_version" ELSE 2
PATCH_IF (("%adjust_speed%" = 1) AND ("%eff_version%" < 2)) BEGIN
  READ_LONG  0x2b0 "known_offset"
  READ_LONG  0x2b8 "slots_offset"
  READ_LONG  0x2bc "items_offset"
  READ_LONG  0x2c4 "effects_offset"
  READ_LONG  0x2c8 "num_effects"
  WRITE_LONG 0x2c8 ("%num_effects%" + 1)
    PATCH_IF ("%eff_version%" = 0) BEGIN
      PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b0 ("%known_offset%" + 48)
      END
      PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b8 ("%slots_offset%" + 48)
      END
      PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2bc ("%items_offset%" + 48)
      END
      INSERT_BYTES  ("%effects_offset%"     ) 48
      WRITE_SHORT ("%effects_offset%"     ) 176
      WRITE_BYTE  ("%effects_offset%" +  2) 1 //  target=self
      WRITE_LONG  ("%effects_offset%" +  4) 0xfffffffd //  walking speed
      WRITE_LONG  ("%effects_offset%" +  8) 0 //  modifier type inc/dec
      WRITE_BYTE  ("%effects_offset%" + 12) 9 //  timing mode permanent
      WRITE_BYTE  ("%effects_offset%" + 18) 100
    END ELSE BEGIN
      PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b0 ("%known_offset%" + 264)
      END
      PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b8 ("%slots_offset%" + 264)
      END
      PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2bc ("%items_offset%" + 264)
      END
      INSERT_BYTES  ("%effects_offset%"     ) 264
      WRITE_LONG  ("%effects_offset%" +  8) 176
      WRITE_LONG  ("%effects_offset%" + 12) 1
      WRITE_LONG  ("%effects_offset%" + 20) 0xfffffffd
      WRITE_LONG  ("%effects_offset%" + 24) 0
      WRITE_LONG  ("%effects_offset%" + 28) 9
      WRITE_SHORT ("%effects_offset%" + 36) 100
    END
END
BUT_ONLY_IF_IT_CHANGES
